!
!  Model of panel with regularly distributed coated balls
!
/filname, PanelModelEpoxy_2, 0
/prep7
!
! Element types
!
!
! Steel ball
!
type_ball = 1
real_ball = 1
et, type_ball, solid186   ! Steel ball
keyopt, type_ball, 2, 0   ! Uniform reduced integration
keyopt, type_ball, 3, 0   ! Structural solid
keyopt, type_ball, 6, 0   ! Pure displacement formulation
keyopt, type_ball, 10, 0  ! No initial stress
r, real_ball
!
! Silicone coating
!
type_coating = 2
real_coating = 2
et, type_coating, solid186   ! Silicone coating
keyopt, type_coating, 2, 0   ! Uniform reduced integration
keyopt, type_coating, 3, 0   ! Structural solid
keyopt, type_coating, 6, 1   ! Mixed formulation
keyopt, type_coating, 10, 0  ! No initial stress
r, real_coating
!
! Epoxy matrix
!
type_matrix = 3
real_matrix = 3
et, type_matrix, solid186   ! Expoxt matrix
keyopt, type_matrix, 2, 0   ! Uniform reduced integration
keyopt, type_matrix, 3, 0   ! Structural solid
keyopt, type_matrix, 6, 0   ! Pure displacement formulation
keyopt, type_matrix, 10, 0  ! No initial stress
r, real_matrix
!
! Rubber ring (should be replaced with a foundation stiffness)
!
type_ring = 4
real_ring = 4
et, type_ring, solid186   ! Rubber ring
keyopt, type_ring, 2, 0   ! Uniform reduced integration
keyopt, type_ring, 3, 0   ! Structural solid
keyopt, type_ring, 6, 1   ! Mixed formulation
keyopt, type_ring, 10, 0  ! No initial stress
r, real_ring
!
! Material parameters
!
! Steel ball
!
mat_ball = 1
E_ball = 207e9
nu_ball = 0.3
rho_ball = 7780
mp, ex, mat_ball, E_ball
mp, nuxy, mat_ball, nu_ball
mp, dens, mat_ball, rho_ball
!
! Silicone coating
!
mat_coating = 2
E_coating = 1.175e5
nu_coating = 0.469
rho_coating = 1300
mp, ex, mat_coating, E_coating
mp, nuxy, mat_coating, nu_coating
mp, dens, mat_coating, rho_coating
!
! Epoxy matrix
!
mat_matrix = 3
E_matrix = 4.35e9
nu_matrix = 0.368
rho_matrix = 1180
mp, ex, mat_matrix, E_matrix
mp, nuxy, mat_matrix, nu_matrix
mp, dens, mat_matrix, rho_matrix
!
! Rubber ring (should be replaced with a foundation stiffness)
!
mat_ring = 4
E_ring = 2e7
nu_ring = 0.49
rho_ring = 1000
mp, ex, mat_ring, E_ring
mp, nuxy, mat_ring, nu_ring
mp, dens, mat_ring, rho_ring
!
! Geometry parameters
!
panel_len = 12.7e-2
panel_thick = 1.9e-2
ball_dia = 6.0e-3
coating_dia = 15.0e-3
!
tube_dia = 8.9e-2
!
ring_dia = 10.5e-2
ring_thick = 3.5e-3
!
! Create geometry
!
! Create left half of the block
!
block, 0, panel_len/2, 0, panel_len/2, -panel_thick/2, 0
!
! Create the right half of the block
!
block, 0, panel_len/2, 0, panel_len/2, 0, panel_thick/2
!
!  Create the areas over which the load is applied and measurement is made
!
k, 1000, 0, 0, panel_thick/2
k, 1001, tube_dia/2, 0, panel_thick/2
k, 1002, 0, tube_dia/2, panel_thick/2
lsel, none
asel, none
circle, 1000, tube_dia/2, , , 90
l, 1000, 1001
l, 1002, 1000
nummrg, kp
al, all
asel, a, loc, z, panel_thick/2
aptn, all
!
lsel, none
asel, none
k, 1100, 0, 0, -panel_thick/2
k, 1101, tube_dia/2, 0, -panel_thick/2
k, 1102, 0, tube_dia/2, -panel_thick/2
circle, 1100, tube_dia/2, , , 90
l, 1100, 1101
l, 1102, 1100
nummrg, kp
al, all
asel, a, loc, z, panel_thick/2
aptn, all
!
! Glue everything
!
vglue, all
allsel
numcmp, volu
!
!  Create the rings
!
lsel, none
asel, none
k, 1000, 0, 0, panel_thick/2
k, 2000, ring_dia/2, 0, panel_thick/2
k, 2001, ring_dia/2+ring_thick/2, 0, panel_thick/2
k, 2002, 0, ring_dia/2, panel_thick/2
k, 2003, 0, ring_dia/2+ring_thick/2, panel_thick/2
circle, 1000, ring_dia/2, , , 90
circle, 1000, ring_dia/2+ring_thick/2, , , 90
l, 2000, 2001
l, 2002, 2003
nummrg, kp
al, all
vext, all, , , , , ring_thick/4
!
lsel, none
asel, none
k, 1100, 0, 0, -panel_thick/2
k, 2100, ring_dia/2, 0, -panel_thick/2
k, 2101, ring_dia/2+ring_thick/2, 0, -panel_thick/2
k, 2102, 0, ring_dia/2, -panel_thick/2
k, 2103, 0, ring_dia/2+ring_thick/2, -panel_thick/2
circle, 1100, ring_dia/2, , , 90
circle, 1100, ring_dia/2+ring_thick/2, , , 90
l, 2100, 2101
l, 2102, 2103
nummrg, kp
al, all
vext, all, , , , , -ring_thick/4
allsel
!
! Glue everything
!
vglue, all
!
! Set the attributes of the matrix
!
asel, s, loc, x, panel_len/2
vsla, s, 0
vatt, mat_matrix, real_matrix, type_matrix
allsel
!
! Set the attributes of the ring
!
asel, s, loc, z, panel_thick/2+ring_thick/4
vsla, s, 0
vatt, mat_ring, real_ring, type_ring
asel, s, loc, z, -panel_thick/2-ring_thick/4
vsla, s, 0
vatt, mat_ring, real_ring, type_ring
allsel
save, PanelModelEpoxy_2_geom, db
!
! Mesh the volumes
!
smrtsize, 2
mopt, amesh, defa
mopt, qmesh, defa
mopt, vmesh, defa
mopt, timp, 1
mopt, pyra, on
mopt, aord, 0
mopt, split, 1
mshkey, 0
mshmid, 0
mshpattern, 0
keyw, accept, 0
mshape, 0, 2d
mshape, 1, 3d
!esize, panel_thick/6
vmesh,all
save, PanelModelEpoxy_2_mesh, db
