!
! Resume the run
!
resume,'PanelModel3_mesh','db','',,
/filname, ModalPanelModel3_1_qtr, 0
!
/prep7
!
! Change ring material properties
!
mat_ring = 4
E_ring = 1.0e6
nu_ring = 0.45
rho_ring = 1200
mp, ex, mat_ring, E_ring
mp, nuxy, mat_ring, nu_ring
mp, dens, mat_ring, rho_ring
!
! Apply boundary conditions
!
!
!  Fix the rings
!
nsel, s, loc, z, panel_thick/2+ring_thick/4 
d, all, uz, 0   
nsel, s, loc, z, -(panel_thick/2+ring_thick/4)  
d, all, uz, 0   
!
!  Fix the symmetry boundaries
!
nsel, s, loc, x, 0.0
d, all, ux, 0
nsel, s, loc, y, 0.0
d, all, uy, 0
allsel
!
! Apply a distributed load on the surface
!
tube_dia = 8.9e-2
local, 100, 1, 0, 0, 0
csys, 100
nsel, s, loc, z, panel_thick/2
nsel, r, loc, x, 0, tube_dia/2 
f, all, fz, 1.0e-2
csys, 0
allsel
finish
!
! Modal solution
!
/solu
antype, modal
modopt, lanb, 500, 1000, 6000, , 
resvec, on
lumpm, off
pstres, off
outpr, all
save, ModalPanelModel3_1_qtr, db
solve
finish 
!
! Expansion pass
!
/solu
expass, on
mxpand, , 1000, 6000, , , 
outpr, all
outres, all
solve
finish
!
!  Harmonic analysis (mode superposition)
!
!/solu
!antype, harmic
!hropt, full, , , , 
!hropt, msup, , , , 
!hrout, , , 
!resvec, on
!nsubst, 200
!harfrq, 0, 4000
!solve
!finish
